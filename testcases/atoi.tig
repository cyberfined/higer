let
  function read_line(): string =
    let
      var result: string := ""
    in
      while 1 do
        let
          var char := getchar()
        in
          if char = "\n" | char = ""
          then break
          else result := concat(result, char)
        end;
      result
    end

  function atoi(s: string): int =
    let
      var result := 0
      var i := size(s) - 1
      var term := 1
    in
      while i >= 0 do
        let
          var cur := substring(s, i, 1)
        in
          result := result + (ord(cur) - 48) * term;
          term := term * 10;
          i := i - 1
        end;
      result
    end

  function reverse(s: string): string =
    let
      var result := ""
      var i := size(s) - 1
    in
      while i >= 0 do (
        result := concat(result, substring(s, i, 1));
        i := i - 1
      );
      result
    end

  function itoa(num: int): string =
    let
      var result := ""
    in
      while 1 do
        let
          var quot := num / 10
          var rem := num - quot * 10
        in
          result := concat(result, chr(rem + 48));
          if quot = 0
          then break
          else num := quot
        end;
      reverse(result)
    end

  var num := atoi(read_line())
  var output := itoa(num + 1234)
in
  print(output)
end